#+TITLE: Emacs, fonts and fontsets
#+date: 15th March 2019
One problem I've had with Emacs is understanding how the font system
works. It's always been clear that fontsets are the answer to a lot of
my problems, but I really struggled to work out how to use it. Here I
plan to note what I've learned about how to set fonts in Emacs.

* Setting the default
  There appear to be as many ways to set the default font as there are
  users of Emacs, but I've gone with this option:

  #+BEGIN_SRC emacs-lisp
    (set-face-attribute 'default nil :font "Droid Sans Mono")
  #+END_SRC

  This modifies the =default= fontset, therefore setting the font in
  all frames.

* Setting fall-back fonts
  But what if you use the same configuration on multiple boxes, or the
  font you choose doesn't provide all the glyphs you need? By default
  Emacs will search through the existing fonts until it finds one that
  contains the glyph, but this can be a fairly haphazard affair and
  can also be really slow[fn::Emacs has a default fall-back to
  [[http://users.teilar.gr/~g1951d/][Symbola]], so it's a good idea to install it to save searching through
  the entire set of fonts.].

  Emacs allows you to specify fall-backs yourself. To do this for the
  default fontset you could do something like this:

  #+BEGIN_SRC emacs-lisp
    (set-fontset-font t nil "Courier New" nil 'append)
  #+END_SRC

  Using a first argument of =t= means it updates the default fontset.
  It is possible to create other fontsets and use them, but I've never
  had much luck with that and prefer to just modify the default. The
  second argument is the glyph range, which we will come back to
  later. The last argument, =append=, tells Emacs to add this font to
  the end of the fontset, so it will be searched after any other fonts
  in the set. You can also use =prepend=, which puts the font first in
  the list, although after the default set with =set-face-attribute=.

* Setting fonts for specific glyphs
  Back to that second argument, the glyph range one. You can specify a
  single glyph, a range of glyphs, a character set name, or a
  language.

  Say you want to set ğŸ˜Š to use a specific font.

  #+BEGIN_SRC emacs-lisp :results silent
    (set-fontset-font t ?ğŸ˜Š "Segoe UI Emoji")
  #+END_SRC

  Or you can set ranges.

  #+BEGIN_SRC emacs-lisp :results silent
    (set-fontset-font t '(?ğŸ˜Š . ?ğŸ˜) "Segoe UI Emoji")
  #+END_SRC

  You can't set ASCII characters this way, Emacs won't let you.

* Setting fonts for different character sets or languages
  Lets say you deal with Thai writing a lot but your default font
  doesn't support it, or you just really like the look of another font
  for Thai writing.

  Have a look at =script-representative-chars= and
  =list-charset-chars= to see if what you're looking for is listed,
  then use that name. Alternatively, use =describe-char= on one of the
  characters in question and look at the *charset* or *script*
  entries.

  #+BEGIN_SRC emacs-lisp
    (set-fontset-font t 'thai "Noto Sans Thai")
  #+END_SRC

  This can result in enormous speed-ups as Emacs no longer has to run
  through hundreds of fonts looking for a compatible one.

  If you need to set a fall-back font for Thai writing then you just
  do it the same as above.

  #+BEGIN_SRC emacs-lisp
    (set-fontset-font t 'thai "Leelawadee UI" nil 'append)
  #+END_SRC

  The downside of this is that if you're using your configuration on
  machines that don't have these fonts then it won't even bother
  searching for a compatible font and will just give you a row of
  boxes. But don't worry, we can force Emacs to search by using
  =font-spec=.

  #+BEGIN_SRC emacs-lisp
    (set-fontset-font t 'thai (font-spec :script 'thai) nil 'append)
  #+END_SRC

  You can put whatever you want in that call to =font-spec= and Emacs
  will search through the fonts to find something suitable. There's no
  reason why you couldn't use =font-spec= to set a specific font.

  So our completed configuration for Thai writing now looks like this:

  #+BEGIN_SRC emacs-lisp :results silent
    (set-fontset-font t 'thai "Noto Sans Thai")
    (set-fontset-font t 'thai "Leelawadee UI" nil 'append)
    (set-fontset-font t 'thai (font-spec :script 'thai) nil 'append)
  #+END_SRC

  Note that you can only =append= or =prepend= a font when there is
  already something set for that glyph or range, which makes sense,
  but I originally thought I was appending to one big list in the
  fontset rather than to one of a set of lists and couldn't understand
  why it wasn't working.

* How to check if a font is installed
  Rather than relying on the fall-back behaviour, you can check
  whether a font is installed before trying to use it. This is pretty
  straight forward as all available font families are available from
  =font-family-list=[fn::Check out [[http://ergoemacs.org/emacs/emacs_list_and_set_font.html][Xah Lee's Font Setup page]] for more
  information.], so you can just check the list:

  #+BEGIN_SRC emacs-lisp
    (member "Noto Sans" (font-family-list))
  #+END_SRC

* Appendix
  I set up some basic fall-backs to [[https://www.google.com/get/noto/][Noto]] fonts for various languages
  to try speeding up navigating Emacs's Hello file (=C-h h=). Because
  there are no fall-backs set for these fonts, if I used this
  configuration on a computer without the fonts then I'd just see lots
  of empty boxes everywhere, but it might give you a head-start in
  setting up your own configuration.

  #+BEGIN_SRC emacs-lisp
    (set-face-attribute 'default nil :font "Droid Sans Mono")

    ;; Latin
    (set-fontset-font t 'latin "Noto Sans")

    ;; East Asia: ä½ å¥½, æ—©æ™¨, ã“ã‚“ã«ã¡ã¯, ì•ˆë…•í•˜ì„¸ìš”
    ;;
    ;; Make sure you use the right font. See
    ;; https://www.google.com/get/noto/help/cjk/.
    ;;
    ;; This font requires "Regular". Other Noto fonts dont.
    ;; Â¯\_(ãƒ„)_/Â¯
    (set-fontset-font t 'han "Noto Sans CJK SC Regular")
    (set-fontset-font t 'kana "Noto Sans CJK JP Regular")
    (set-fontset-font t 'hangul "Noto Sans CJK KR Regular")
    (set-fontset-font t 'cjk-misc "Noto Sans CJK KR Regular")

    ;; South East Asia: á‡áŸ†ášá¶á”áŸá½áš, àºªàº°àºšàº²àºàº”àºµ, á€™á€„á€ºá€¹á€‚á€œá€¬á€•á€«, à¸ªà¸§à¸±à¸ªà¸”à¸µà¸„à¸£à¸±à¸š
    (set-fontset-font t 'khmer "Noto Sans Khmer")
    (set-fontset-font t 'lao "Noto Sans Lao")
    (set-fontset-font t 'burmese "Noto Sans Myanmar")
    (set-fontset-font t 'thai "Noto Sans Thai")

    ;; Africa: áˆ áˆ‹áˆ
    (set-fontset-font t 'ethiopic "Noto Sans Ethiopic")

    ;; Middle/Near East: ×©×œ×•×, Ø§Ù„Ø³Ù‘Ù„Ø§Ù… Ø¹Ù„ÙŠÙƒÙ…
    (set-fontset-font t 'hebrew "Noto Sans Hebrew")
    (set-fontset-font t 'arabic "Noto Sans Arabic")

    ;;  South Asia: àª¨àª®àª¸à«àª¤à«‡, à¤¨à¤®à¤¸à¥à¤¤à¥‡, à²¨à²®à²¸à³à²•à²¾à²°, à´¨à´®à´¸àµà´•à´¾à´°à´‚, à¬¶à­à¬£à¬¿à¬¬à­‡,
    ;;              à¶†à¶ºà·”à¶¶à·à·€à¶±à·Š, à®µà®£à®•à¯à®•à®®à¯, à°¨à°®à°¸à±à°•à°¾à°°à°‚, à½–à½€à¾²à¼‹à½¤à½²à½¦à¼‹à½–à½‘à½ºà¼‹à½£à½ºà½‚à½¦à¼
    (set-fontset-font t 'gujarati "Noto Sans Gujarati")
    (set-fontset-font t 'devanagari "Noto Sans Devanagari")
    (set-fontset-font t 'kannada "Noto Sans Kannada")
    (set-fontset-font t 'malayalam "Noto Sans Malayalam")
    (set-fontset-font t 'oriya "Noto Sans Oriya")
    (set-fontset-font t 'sinhala "Noto Sans Sinhala")
    (set-fontset-font t 'tamil "Noto Sans Tamil")
    (set-fontset-font t 'telugu "Noto Sans Telugu")
    (set-fontset-font t 'tibetan "Noto Sans Tibetan")
  #+END_SRC

Updated 31/3/2019. Thanks to Yuan Fu for his suggestions.
